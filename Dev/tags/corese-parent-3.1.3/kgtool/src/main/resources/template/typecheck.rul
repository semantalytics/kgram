<?xml version='1.0' encoding='UTF-8'?>
<!--
SPARQL Template Pretty Printer
Corese/KGRAM - Wimmics Inria I3S - 2013
-->
<rdf:RDF  xmlns:rdf='http://www.w3.org/1999/02/22-rdf-syntax-ns#' 
  xmlns='http://ns.inria.fr/edelweiss/2011/rule#'>
<rule>
<body>
<![CDATA[
#
# Olivier Corby, Wimmics Inria I3S, 2013
#
prefix sp: <http://spinrdf.org/sp#>
template kg:start {
  kg:pprintAll(?in)
  ; separator = "\n"
}
where {
  ?in sp:where ?w
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Olivier Corby, Wimmics Inria I3S, 2013
#
prefix sp: <http://spinrdf.org/sp#>
template {
  "Property " kg:uri(?p) " unknown in target graph"
    ; separator = "\n"

}
where {
  select distinct  ?p ?in
  where {
    ?in sp:where/(! sp:undef)+ [
      sp:subject   ?s ;
      sp:predicate ?p ;
      sp:object    ?o
    ]
    filter (
      ! isBlank(?p) && not exists { ?x ?p ?y }
    )
  }
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Cound nb of occurrences of SPIN Query triples in target graph
#
# Olivier Corby, Wimmics Inria I3S, 2013
#
prefix sp: <http://spinrdf.org/sp#>
template {
  if(bound(?c), ?c, 0)
  " : " kg:pprintWith(pp:spin, ?t) 
  ; separator = "\n"
}
where {

 ?in sp:where/(! sp:undef)+ ?t
      ?t sp:predicate ?p

  {select (count (*) as ?c) ?t
    where {
	
      ?t sp:subject  ?s ;
	sp:predicate ?p ;
	sp:object    ?o      
      
      { filter(isBlank(?s)   && isBlank(?o))   . [] ?p [] } union
      { filter(isBlank(?s)   && ! isBlank(?o)) . [] ?p ?o } union
      { filter(! isBlank(?s) && isBlank(?o))   . ?s ?p [] } union
      { filter(! isBlank(?s) && ! isBlank(?o)) . ?s ?p ?o }  
  
    } 
    group by ?t
  }
  
}
order by desc(?c) ?p
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Embed statements of select where inside optional
# Olivier Corby, Wimmics Inria I3S, 2013
#
prefix sp: <http://spinrdf.org/sp#>
template {
  "Subject unknown in target graph: " kg:pprintWith(pp:spin, ?t) 
    ; separator = "\n"

}
where {
  ?in sp:where/(! sp:undef)+ ?t
  
  ?t sp:subject  ?s ;
     sp:predicate ?p
  
  filter (
    ! isBlank(?s) && ! isBlank(?p) && not exists { ?s ?p ?x }
  )
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Olivier Corby, Wimmics Inria I3S, 2013
#
prefix sp: <http://spinrdf.org/sp#>
template {
  "Undefined variable '?" ?v "' in " kg:pprintWith(pp:spin, ?f)   
  ; separator = "\n"

}
where {

  ?in sp:where/(! sp:undef)+ ?f
  
  ?f a sp:Filter ;
    (! sp:undef)+ [ sp:varName ?v ]
    
  filter not exists { 
    ?in sp:where/(! sp:undef)+ ?t
    { ?t sp:subject   [ sp:varName ?v ] } union 
    { ?t sp:predicate [ sp:varName ?v ] } union 
    { ?t sp:object    [ sp:varName ?v ] }
  }
  
}
]]>
</body>
</rule>

<rule>
<body>
<![CDATA[
#
# Olivier Corby, Wimmics Inria I3S, 2013
#
prefix sp: <http://spinrdf.org/sp#>
template {
  "Object unknown in target graph: " kg:pprintWith(pp:spin, ?t) 
    ; separator = "\n"

}where {
  ?in sp:where/(! sp:undef)+ ?t
  
  ?t sp:object  ?o ;
     sp:predicate ?p
  
  filter (
    ! isBlank(?o) && ! isBlank(?p) && not exists { ?x ?p ?o }
  )
}
]]>
</body>
</rule>

</rdf:RDF>
